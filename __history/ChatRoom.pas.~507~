unit ChatRoom;
{
- FormCreate, CreateWithRoom, FormClose
UI 구성 & 초기화 (방의 UI 컴포넌트 생성 및 이벤트 핸들러 연결)

- btnSendClick, HandleServerMessage, HandleChatMessage, AddMessage
메시지 송신 / 수신 처리 (채팅 입력 → JSON 생성 → 서버 송신 / 서버 수신 → 화면 표시)

- HandleUserList, HandleExitRoom, HandleNoticeMessage
	유저 입퇴장 및 인원 관리 (서버에서 유저 리스트나 입퇴장 알림 수신 시 처리)

- MenuInviteClick, RequestUserList, HandleInviteAvailableUsers, BtnInviteClick
초대 기능 (초대 가능한 사용자 요청 및 선택 UI 표시)

- GetUserImage, HashStringDJB2, MemoMessageKeyDown
유틸리티 / 입력 보조 기능

-코드 흐름
1. MainForm에서 AddChatRoom 호출

2. TChatRoomForm.CreateWithRoom
     2-1 FormCreate → UI 초기화
     2-2 서버 이벤트 연결
     2-3 USER_LIST 요청

==== 사용자 채팅 중 ====
1. 메시지 입력 → btnSendClick → 서버 전송
2. 서버 → 모든 클라이언트에 CHAT 전송
3. HandleChatMessage → AddMessage(왼쪽/오른쪽)

      3-1. 입퇴장 → HandleExitRoom / HandleNoticeMessage
      3-2. 초대 메뉴 → RequestUserList → HandleInviteAvailableUsers

[폼 닫기 시]
FormClose → 이벤트 해제 → caFree

}

interface

uses
   Winapi.Windows, Winapi.Messages, System.SysUtils, System.Classes,
   Vcl.Forms, Vcl.Controls, Vcl.StdCtrls, Vcl.ExtCtrls, Vcl.Graphics,
   Vcl.Dialogs, Vcl.Menus, Vcl.Buttons, Vcl.Imaging.pngimage, Data, Data.DBXJSON,
   Vcl.CheckLst, Math;


type
   TChatRoomForm = class(TForm)
      PanelTop                         : TPanel;         // 상단 패널 (방 정보, 유저 이미지)
      btnMore                          : TSpeedButton;   // 메뉴 버튼 (더보기)
      PopupMenu1                       : TPopupMenu;     // 메뉴
      MenuExit, MenuLeave, MenuInvite  : TMenuItem;

      PanelBottom                      : TPanel;         // 하단 패널 (메시지 입력창, 전송 버튼)
      btnSend                          : TButton;

      ScrollBoxMessages                : TScrollBox;     // 채팅 메시지 표시 영역
      MemoMessage                      : TMemo;          // 프로필 이미지
      lblRoomName                      : TLabel;         // 방 이름 표시
      lblUserCount                     : TLabel;         // 방 유저 수 표시


      procedure FormCreate          (Sender: TObject);
      procedure btnSendClick        (Sender: TObject);
      procedure btnMoreClick        (Sender: TObject);
      procedure MenuExitClick       (Sender: TObject);
      procedure MenuLeaveClick      (Sender: TObject);
      procedure MenuInviteClick     (Sender: TObject);
      procedure BtnInviteClick      (Sender: TObject);

      function  GetUserImage        (const UserName: string): string;

      procedure MemoMessageKeyDown  (Sender: TObject; var Key: Word;
                                    Shift: TShiftState);
      procedure MemoMessageKeyPress (Sender: TObject; var Key: Char);

   private
      FUserImages          : TArray<string>;    // 사용자 이미지 배열

      InvitePanel          : TPanel;
      CLBAvailableUsers    : TCheckListBox;     //초대 가능한 유저를 표시해주는 리스트
      BtnInvite            : TButton;
      FMyUserName          : string;            // 내 이름
      FCurrentRoomUsers    : TStringList;       // 현재 방 유저 리스트
      FCurrentRoomName     : string;            // 현재 방 이름

      procedure AddMessage(const UserName, Msg: string; IsMine: Boolean; const UserImg: string = ''; IsSystem: Boolean = False);

   public
      UserCount: Integer;	//현재 방 유저수
      constructor CreateWithRoom             (AOwner: TComponent; const ACurrentRoomName: string);
      procedure UpdateUserImageUI;

      procedure HandleChatMessage            (Sender: TObject; const Msg: string);
      procedure HandleChatHistory            (Sender: TObject; const Msg: string);
      procedure HandleExitRoom               (Sender: TObject; const Msg: string);
      procedure HandleUserList               (Sender: TObject; const Msg: string);

      procedure HandleInviteAvailableUsers   (Sender: TObject; const Msg: string);

      procedure SendUserListRequest          (const RoomName: string);  //INDEX 와 USER_LIST 동시성 문제로 인해 INDEX 처리 후 USER_LIST 요청
      procedure UpdateAvailableUsers         (const Users: TArray<string>);
      procedure RequestUserList;
      procedure AddUser                      (const UserName: string);

      property UserImages        : TArray<string> read FUserImages write FUserImages;
      property CurrentRoomName   : string read FCurrentRoomName write FCurrentRoomName;	//서버와 동기화된 방 이름
      property CurrentRoomUsers  : TStringList read FCurrentRoomUsers write FCurrentRoomUsers;
      property MyUserName        : string read FMyUserName write FMyUserName;

   end;

var
   ChatRoomForm: TChatRoomForm;

implementation

{$R *.dfm}

uses Main, Connect, UserInfo;

{ 간단한 DJB2 해시 함수 (항상 같은 문자열에 대해 같은 결과) }
function HashStringDJB2(const S: string): Cardinal;
var
   i: Integer;
begin
   Result := 5381;
   for i := 1 to Length(S) do
      Result := ((Result shl 5) + Result) + Cardinal(Ord(S[i])); // Result * 33 + c
end;

procedure TChatRoomForm.FormCreate(Sender: TObject);
begin
   // ================= Top Panel 기본 설정 =================
   PanelTop.Align          := alTop;
   PanelTop.Height         := 70;

   lblRoomName.Left        := 70;
   lblRoomName.Top         := 10;
   lblRoomName.Font.Size   := 16;
   lblRoomName.Font.Style  := [fsBold];

   lblUserCount.Left       := 70;
   lblUserCount.Top        := 35;
   lblUserCount.Font.Size  := 12;
   lblUserCount.Font.Color := clGray;
   lblUserCount.Caption := 'Users: 1';//09/30 첫입장시 유저수 가비지값 => 업데이트 전 값 출력

   PanelTop.BringToFront;

   // ================= Bottom Panel 기본 설정 =================
   PanelBottom.Align       := alBottom;
   PanelBottom.Height      := 60;

   btnSend.Align           := alRight;
   btnSend.Caption         := '전송';

   MemoMessage.ScrollBars  := ssVertical;
   MemoMessage.WordWrap    := True;
   MemoMessage.Lines.Clear;

  // ================= ScrollBox 설정 =================
   ScrollBoxMessages.Align                   := alClient;
   ScrollBoxMessages.VertScrollBar.Position  := ScrollBoxMessages.VertScrollBar.Range;     //가장 밑으로 내려가게
   ScrollBoxMessages.HorzScrollBar.Visible   := False;                                     //가로 스크롤 숨김
   // ================= PopupMenu 연결 =================
   MenuExit.OnClick     := MenuExitClick;
   MenuLeave.OnClick    := MenuLeaveClick;
   MenuInvite.OnClick   := MenuInviteClick;
end;

 //=========
constructor TChatRoomForm.CreateWithRoom(AOwner: TComponent; const ACurrentRoomName: string);

begin
   inherited Create(AOwner); // FormCreate 자동 호출됨
   CurrentRoomName := ACurrentRoomName;           //메인폼에서 전달받은 방 이름을 저장
   // 방 이름
   lblRoomName.Caption  := ACurrentRoomName;      //UI에도 저장(채팅방 상단)
end;


// 메시지 전송
procedure TChatRoomForm.btnSendClick(Sender: TObject);
var
   JSONObj  : TJSONObject;
begin

   if Trim(MemoMessage.Text) = '' then     //nil값 전송x
      Exit;

   JSONObj := TJSONObject.Create;
   try
      JSONObj.AddPair('command', TJSONString.Create('CHAT'));              //객체에 command 키와 CHAT 을 담는다
      JSONObj.AddPair('room', TJSONString.Create(FCurrentRoomName));
      JSONObj.AddPair('message', TJSONString.Create(MemoMessage.Text));

      if Assigned(Con) and Con.Client.Connected then                      //Con이 유효하고 서버워 연결이 되어 있다면
         Con.SendJson('CHAT', [                                           //서버에 전송
         'room', FCurrentRoomName,
         'message', MemoMessage.Text
         ]);

      MemoMessage.Clear;                                                  //입력창 지운다
   finally
      JSONObj.Free;
   end;
end;

procedure TChatRoomForm.HandleChatMessage(Sender: TObject; const Msg: string);
var
   JSONObject                                : TJSONObject;
   UserName, ChatMsg, RoomName, Cmd, Status  : string;
   IsMine                                    : Boolean;
begin
   JSONObject := TJSONObject.ParseJSONValue(Msg) as TJSONObject;
   try
      if JSONObject = nil then Exit;

      // 모든 필요한 정보를 추출
      Status   := SafeGetValue(JSONObject, 'status');
      Cmd      := SafeGetValue(JSONObject, 'command');
      UserName := SafeGetValue(JSONObject, 'username', 'unknown');
      ChatMsg  := SafeGetValue(JSONObject, 'message', '');
      RoomName := SafeGetValue(JSONObject, 'room');


      if not SameText(FCurrentRoomName, RoomName) then       //  요청 보냈던  방이름과 서버에서 받은 방이름  일치하는지 확인
         Exit;


      if SameText(Cmd, 'CHAT') then                         //  'CHAT' 커맨드이고
      begin

         if SameText(Status, 'MSG') then                   // 'MSG' 상태일때
         begin
            // status가 'MSG'이면 일반 채팅으로 처리
            IsMine := SameText(UserInfo.CurrentUserName, UserName);    //본인이면 True
            AddMessage(UserName, ChatMsg, IsMine);                     //채팅UI 추가
         end
         else if SameText(Status, 'INFO') then                         //INFO 일시 서버 알림
         begin
            // status가 'INFO'이면 시스템 메시지로 처리
            AddMessage('', ChatMsg, False, '', True);                  //4번쨰가 유저 이미지 5번째가 시스템알림인지
         end;
      end;
   finally
      JSONObject.Free;
   end;
end;

//===== 채팅이력 ========
procedure TChatRoomForm.HandleChatHistory(Sender: TObject; const Msg: string);   //추석 수정해야함, 메세지 타입을 추가시켜서 시스템 알림도 채팅처럼 만들어서 채팅이력에도 포함되게
var
   JSONObject, MsgObj            : TJSONObject;
   ChatArray                     : TJSONArray;
   UserName, ChatMsg, RoomName   : string;
   status                        : string;
   IsMine                        : Boolean;
   i                             : Integer;

begin
   JSONObject := TJSONObject.ParseJSONValue(Msg) as TJSONObject;         //서버에서 받은 Msg값을 객체로 형변환해서 저장
   try
      if JSONObject = nil then Exit;

      // 방 이름 먼저 확인
      RoomName := SafeGetValue(JSONObject, 'room');                     // 서버에서 받은 방이름 저장
      if not SameText(RoomName, CurrentRoomName) then                   //서버에서 받은 방이름과 요청보냈던 방이름이 일치하는지 확인
         Exit;

      ChatArray := JSONObject.GetValue('history') as TJSONArray;        //history 키의 값을 배열로 형변환해서 저장한다
      if ChatArray = nil then Exit;

      for i := 0 to ChatArray.Size - 1 do                               //채팅이력 만큼 반복해서
      begin
         MsgObj      := ChatArray.Get(i) as TJSONObject;                //배열을 객체로 변환시켜 저장한다
         if MsgObj = nil then Continue;

         // 서버가 주는 메시지 타입을 확인
         status      := SafeGetValue(MsgObj, 'status', 'MSG');
         ChatMsg     := SafeGetValue(MsgObj, 'message', '');

         // 상태가 'INFO'면 시스템 메시지로 표시
         if status = 'INFO' then
         begin
            AddMessage('', ChatMsg, False, '', True); // IsSystem=True
         end
         else // 상태가 'MSG'이거나 없으면 일반 채팅으로 표시
         begin
            UserName := SafeGetValue(MsgObj, 'username', 'unknown');     //유저 이름의 값이 비어있으면 unknown 으로 표시
            IsMine   := SameText(UserInfo.CurrentUserName, UserName);    //로그인했을때 저장한 사용자 이름과 방금 추출한 사용자 이름 비교
            AddMessage(UserName, ChatMsg, IsMine);
         end;
      end;
      // 히스토리 처리 끝난 뒤 유저 목록 요청(09/30 INDEX 와 USER_LIST 동시성 문제로 인해 INDEX 처리 후 USER_LIST 요청
      if ChatArray.Size > 0 then     //채팅이력이 있으면
         SendUserListRequest(CurrentRoomName);


   finally
      JSONObject.Free;
   end;
end;

//==========유저 목록 요청=========
procedure TChatRoomForm.SendUserListRequest(const RoomName: string);
begin
   try

      // 서버에 전송
      Con.SendJson('USER_LIST', ['room', RoomName]);
   finally

   end;
end;

// 채팅방에 접속한 유저 수
procedure TChatRoomForm.HandleUserList(Sender: TObject; const Msg: string);
var
   JSONObject: TJSONObject;
   UsersArray: TJSONArray;
   i         : Integer;
   UserName  : string;
begin
   JSONObject := TJSONObject.ParseJSONValue(Msg) as TJSONObject;
   try
      if JSONObject = nil then Exit;

      if CurrentRoomUsers = nil then
         CurrentRoomUsers := TStringList.Create;

      // CurrentRoomUsers 초기화
      CurrentRoomUsers.Clear;

      // 서버에서 받은 유저 이름 배열 처리
      UsersArray := JSONObject.GetValue('users') as TJSONArray;       //요청한 방 안에이는 모든유저 이름을 배열에 저장하고
      if Assigned(UsersArray) then
         for i := 0 to UsersArray.Size - 1 do                        //채팅방에 있는 유저수 만큼 반복
         begin
            UserName := UsersArray.Get(i).Value;
            CurrentRoomUsers.Add(UserName);                         //리스트에 저장
         end;

      // 숫자 갱신
      Self.UserCount := CurrentRoomUsers.Count;

      // 레이블 업데이트
      if Assigned(lblUserCount) then
         lblUserCount.Caption := Format('Users: %d', [UserCount]);

      UpdateUserImageUI;       //채팅방 상단에 있는 사진 업데이트

   finally
      JSONObject.Free;
   end;
end;


 //채팅창에 채팅 UI
procedure TChatRoomForm.AddMessage(  const UserName, Msg: string; IsMine: Boolean;
                                     const UserImg: string = ''; IsSystem: Boolean = False);
var
   MsgPanel                                           : TPanel;
   lblMsg, lblTime, lblName,lblSys                    : TLabel;
   Img                                                : TImage;
   MaxWidth, Padding, TextWidth, TextHeight, YPos, i  : Integer;
   TimeStr, Line, ImgFile, Image_Folder               : string;

begin
   Padding  := 12;      // 말풍선 안 여백
   MaxWidth := 200;     // 메시지 최대 너비

  // Y 위치 계산
   if ScrollBoxMessages.ControlCount > 0 then
      YPos :=  ScrollBoxMessages.Controls[ScrollBoxMessages.ControlCount - 1].Top +
               ScrollBoxMessages.Controls[ScrollBoxMessages.ControlCount - 1].Height + 5
   else
      YPos := 5;

   // 메시지 패널 생성
   MsgPanel                   := TPanel.Create(ScrollBoxMessages);
   MsgPanel.Parent            := ScrollBoxMessages;
   MsgPanel.BevelOuter        := bvNone;
   MsgPanel.SetBounds(0, YPos, ScrollBoxMessages.Width, 1);
   MsgPanel.Align             := alNone;
   MsgPanel.Width             := ScrollBoxMessages.Width; // 스크롤바 무시한 고정 폭
   MsgPanel.Top               := YPos;
   MsgPanel.Margins.Right     := 20;  // 스크롤바 여백 확보


     // 시스템 메시지인 경우
   if IsSystem then
   begin
      lblSys              := TLabel.Create(MsgPanel);
      lblSys.Parent       := MsgPanel;
      lblSys.Caption      := Msg;
      lblSys.WordWrap     := True;
      lblSys.Alignment    := taCenter;
      lblSys.Font.Style   := [fsItalic];
      lblSys.Font.Color   := clGray;
      lblSys.Transparent  := True;
      lblSys.AutoSize     := True;

      // 라벨을 패널 중앙으로 이동
      lblSys.Left := (MsgPanel.Width - lblSys.Width) div 2;

      MsgPanel.Height := lblSys.Height + 2 * Padding;

   end
   else //유저 채팅인 경우
   begin
      // 일반 메시지 라벨 생성
   lblMsg            := TLabel.Create(MsgPanel);
   lblMsg.Parent     := MsgPanel;
   lblMsg.WordWrap   := True;
   lblMsg.Caption    := Msg;
   lblMsg.Font.Size  := 12;
   lblMsg.Alignment  := taLeftJustify; // 항상 왼쪽부터 글 시작
   lblMsg.Transparent:= False;         //배경색 보이게

   // 글자 폭 계산
   TextWidth := lblMsg.Canvas.TextWidth(Msg) + Padding;
   if TextWidth < 50 then TextWidth := 50;
   if TextWidth > MaxWidth then TextWidth := MaxWidth;

   lblMsg.Width := TextWidth;

   // 줄바꿈 높이 계산
   TextHeight := lblMsg.Canvas.TextHeight(Msg) * ((lblMsg.Canvas.TextWidth(Msg) div MaxWidth) + 1);
   lblMsg.Height := TextHeight + Padding;

   // 줄바꿈 처리
   lblMsg.Caption := '';
   Line := '';
   for i := 1 to Length(Msg) do
   begin
      Line := Line + Msg[i];
      if lblMsg.Canvas.TextWidth(Line) > MaxWidth - 10 then  // 여백 10 포함
      begin
         lblMsg.Caption := lblMsg.Caption + Line + #13#10;   //
         Line := '';
      end;
   end;
   lblMsg.Caption := lblMsg.Caption + Line;


   // 시간 라벨
   TimeStr              := FormatDateTime('hh:nn', Now);
   lblTime              := TLabel.Create(MsgPanel);
   lblTime.Parent       := MsgPanel;
   lblTime.Caption      := TimeStr;
   lblTime.Font.Size    := 8;
   lblTime.Font.Color   := clBlack;
   lblTime.Transparent  := False;

   if IsMine then
   begin
      // 오른쪽 정렬 (본인)
      lblMsg.Left    := ScrollBoxMessages.Width - lblMsg.Width - Padding*2;
      lblMsg.Top     := Padding;
      lblMsg.Color   := clSkyBlue;
      lblMsg.Font.Color := clWhite;

      lblTime.Left   := lblMsg.Left - lblTime.Width - 3;
      lblTime.Top    := lblMsg.Top + lblMsg.Height - lblTime.Height;
   end
   else //(상대)
   begin
    // 프로필 이미지
      Img         := TImage.Create(MsgPanel);
      Img.Parent  := MsgPanel;
      Img.SetBounds(Padding, Padding, 40, 40);
      Img.Stretch := True;

      ImgFile := UserImg;   //서버가 준 이미지 파일 저장
      Image_Folder := IncludeTrailingPathDelimiter(ExtractFilePath(Application.ExeName) + 'image');

      // 서버가 준 이미지가 없거나, 해당 파일이 존재하지 않으면
      if (ImgFile = '') or not FileExists(Image_Folder + ImgFile) then
      begin
         //  GetUserImage를 호출하여 사용자별 기본 이미지를 가져옴
         ImgFile := GetUserImage(UserName);
      end;

      //  최종 결정된 이미지 파일을 로드
      if FileExists(Image_Folder + ImgFile) then
         Img.Picture.LoadFromFile(Image_Folder + ImgFile);

    // 이름 라벨
      lblName              := TLabel.Create(MsgPanel);
      lblName.Parent       := MsgPanel;
      lblName.Caption      := UserName;
      lblName.Font.Style   := [fsBold];
      lblName.SetBounds(Img.Left + Img.Width + 5, Padding, MsgPanel.Width - Img.Width - 20, 15);

      // 왼쪽 정렬
      lblMsg.Left       := Img.Left + Img.Width + 5;
      lblMsg.Top        := lblName.Top + lblName.Height + 2;
      lblMsg.Alignment  := taLeftJustify;
      lblMsg.Color      := clWhite;
      lblMsg.Font.Color := clBlack;

      lblTime.Left      := lblMsg.Left + lblMsg.Width + 3;
      lblTime.Top       := lblMsg.Top + lblMsg.Height - lblTime.Height;
   end;

   // 채팅 과 채팅 사이 높이
   MsgPanel.Height      := lblMsg.Top + lblMsg.Height ;
   end;

   // 스크롤 아래쪽
   ScrollBoxMessages.VertScrollBar.Position  := ScrollBoxMessages.VertScrollBar.Range;
   ScrollBoxMessages.HorzScrollBar.Visible   := False;
end;




//기본 이미지
function TChatRoomForm.GetUserImage(const UserName: string): string;
const
   DEFAULT_COUNT = 4; // 기본 이미지 갯수 (default1..default4)
var
   HashVal     : Cardinal;
   ImgIndex    : Integer;
begin
   if UserName = '' then
   begin
      Result   := 'default_profile.png';
      Exit;
   end;

   HashVal     := HashStringDJB2(UserName);
   ImgIndex    := (HashVal mod DEFAULT_COUNT) + 1;
   Result      := Format('default_profile%d.png', [ImgIndex]);

end;

// KeyDown 이벤트
procedure TChatRoomForm.MemoMessageKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
begin
   if (Key = VK_RETURN) and not (ssShift in Shift) then
   begin
      btnSendClick(btnSend);  // 전송 버튼 클릭 처리
      Key := 0;               // 기본 줄바꿈 막기 (1차)
   end;
end;

// KeyPress 이벤트
procedure TChatRoomForm.MemoMessageKeyPress(Sender: TObject; var Key: Char);
begin
   if (Key = #13) and not (ssShift in KeyDataToShiftState(GetKeyState(VK_SHIFT))) then
   begin
      Key := #0;  // 줄바꿈 문자 막기 (2차)
   end;
end;


procedure TChatRoomForm.btnMoreClick(Sender: TObject);
begin
   // PopupMenu를 버튼 위치에 맞춰 표시
   if Assigned(PopupMenu1) then
      PopupMenu1.Popup(
      btnMore.ClientOrigin.X,                   // 버튼 왼쪽 기준
      btnMore.ClientOrigin.Y + btnMore.Height   // 버튼 아래쪽
    );
end;

// 더보기 메뉴 클릭 이벤트 예시
procedure TChatRoomForm.MenuExitClick(Sender: TObject);

begin
   // 서버에 퇴장 명령 전송
   if Assigned(Con) and Con.Client.Connected then
      Con.SendJson('EXIT', ['room', FCurrentRoomName]);

end;
//서버에서 퇴장 명령 수신
procedure TChatRoomForm.HandleExitRoom(Sender: TObject; const Msg: string);
var
   JSONObject   : TJSONObject;
   ExitingUser  : string;
begin
   JSONObject   := TJSONObject.ParseJSONValue(Msg) as TJSONObject;
   try
      if JSONObject = nil then Exit;

      ExitingUser := SafeGetValue(JSONObject, 'username');   //나간 유저 이름 저장

      if ExitingUser = UserInfo.CurrentUserName then         //나간 유저 이름과 본인 이름이 같을시
      begin
         // 자기 자신 퇴장 → 패널 제거 + 폼 닫기
         MainForm.RemoveRoomPanel   (FCurrentRoomName);  // ScrollBox에서 해당 RoomPanel 제거
         MainForm.RemoveRoomFromList(FCurrentRoomName);
         Close;                       // 폼 닫기
      end
      else
      begin
         // 타인 퇴장 → 메시지 표시만
         AddMessage(ExitingUser, 'has left the room.', False, '', True);     //추석 수정,
      end;

   finally
      JSONObject.Free;
   end;
end;

//========방 나가기=======//
procedure TChatRoomForm.MenuLeaveClick(Sender: TObject);
begin
   // 단순 나가기
   Close;
end;

//========= 초대========//
procedure TChatRoomForm.MenuInviteClick(Sender: TObject);
begin
   if Self.CurrentRoomName = '' then        //서버에 요청한 방 이름이 공백이면
   begin
      ShowMessage('먼저 방에 입장해야 초대가 가능합니다.');
      Exit;
   end;
   RequestUserList;                         //유져리스트요청
end;

procedure TChatRoomForm.RequestUserList;
var
   JSONObject  : TJSONObject;
begin
   JSONObject  := TJSONObject.Create;
   try
      // (요청한 방에 없는) 서버에 접속한 유저 리스트 요청
      Con.SendJson('INVITE_AVAILABLE_USERS', ['room', CurrentRoomName]);
   finally
      JSONObject.Free;
   end;
end;

procedure TChatRoomForm.UpdateAvailableUsers(const Users: TArray<string>);
var
   i        : Integer;
   UserName : string;
begin
   if not Assigned(CLBAvailableUsers) then Exit;

   CLBAvailableUsers.Items.Clear;               //체크리스트박스 요소를 초기화 하고

   for i := 0 to High(Users) do                 //유저수 만큼 반복
   begin
      UserName := Users[i];

      // 자기 자신과 현재 방 유저 제외
      if (UserName <> MyUserName) and (CurrentRoomUsers.IndexOf(UserName) = -1) then
         CLBAvailableUsers.Items.Add(UserName);
   end;
end;

 //======== 채팅방 상단 이미지 ========//
procedure TChatRoomForm.UpdateUserImageUI;
var
   i, ImgX, ImgCount       : Integer;
   GroupProfilePanel       : TGridPanel;
   ImgProfile              : TImage;
   Image_Folder            : string;
   UserName, UserImageFile : string;
   CellPanel               : TPanel; // ★★★ 이 줄을 추가 ★★★
begin
    // 기존에 있던 프로필 이미지를 모두 삭제 (중복 방지)
   for i := PanelTop.ControlCount - 1 downto 0 do
   begin
      if PanelTop.Controls[i] is TGridPanel then
         PanelTop.Controls[i].Free;
   end;

   // (수정) 현재 방의 사용자 수(CurrentRoomUsers.Count)로 ImgCount를 초기화
   if not Assigned(CurrentRoomUsers) then Exit; // 안전장치

   ImgCount := Min(4, CurrentRoomUsers.Count);   // 최대 4개까지만 표시

   if ImgCount = 0 then Exit; // 방에 아무도 없으면 실행 중지

   ImgX := 10;
   Image_Folder := IncludeTrailingPathDelimiter(ExtractFilePath(Application.ExeName) + 'image');

   GroupProfilePanel          := TGridPanel.Create(Self);      //상단 프로필 구역 고정
   GroupProfilePanel.Parent   := PanelTop;
   //컴포넌트 전체 크기
   GroupProfilePanel.SetBounds(ImgX, 10, 50, 50);

   GroupProfilePanel.ShowCaption := False;

   //레이아웃 설정(행/열)
   GroupProfilePanel.ColumnCollection.Clear;
   GroupProfilePanel.RowCollection.Clear;

   if ImgCount = 1 then
   begin
      GroupProfilePanel.ColumnCollection.Add;
      GroupProfilePanel.RowCollection.Add;
   end
   else if ImgCount = 2 then
   begin
      GroupProfilePanel.ColumnCollection.Add;
      GroupProfilePanel.ColumnCollection.Add;
      GroupProfilePanel.RowCollection.Add;
   end
   else
   begin
      GroupProfilePanel.ColumnCollection.Add;
      GroupProfilePanel.ColumnCollection.Add;
      GroupProfilePanel.RowCollection.Add;
      GroupProfilePanel.RowCollection.Add;
   end;

   for i := 0 to ImgCount - 1 do
   begin
// 1. '셀 패널'을 만들고 여백을 강제로 0으로 설정
    CellPanel := TPanel.Create(Self);
    CellPanel.Parent := GroupProfilePanel;
    CellPanel.Align := alClient;
    CellPanel.BevelOuter := bvNone;

    // ★★★ VCL 스타일 방어 코드 ★★★
    CellPanel.Padding.SetBounds(0, 0, 0, 0);
    CellPanel.Margins.SetBounds(0, 0, 0, 0);

    // 2. TImage를 만들고 부모를 CellPanel로 지정
    ImgProfile := TImage.Create(Self);
    ImgProfile.Parent := CellPanel;

    // ★★★ TImage는 alClient 대신 Anchors로 채웁니다. ★★★
    ImgProfile.Align := alNone;
    ImgProfile.Anchors := [akLeft, akTop, akRight, akBottom];
      ImgProfile.Stretch := True;
      ImgProfile.Proportional := True;

      // 목록에서 i번째 사용자 이름을 가져옴
      UserName := CurrentRoomUsers[i];

      // 사용자 이름으로 표시할 이미지 파일 경로
      UserImageFile := GetUserImage(UserName);

      if FileExists(Image_Folder + UserImageFile) then
         ImgProfile.Picture.LoadFromFile(Image_Folder + UserImageFile)
      else if FileExists(Image_Folder + 'default_profile.png') then
         ImgProfile.Picture.LoadFromFile(Image_Folder + 'default_profile.png');



   end;
   Inc(ImgX, GroupProfilePanel.Width + 5);


   // (수정) CurrentRoomUsers 목록을 기준으로 이미지 생성
//   for i := 0 to ImgCount - 1 do
//   begin
//      ImgProfile := TImage.Create(Self); // 주인은 Self
//      ImgProfile.Parent := PanelTop;
//      ImgProfile.SetBounds(ImgX, 10, 50, 50);
//      ImgProfile.Stretch := True;
//
//      // 목록에서 i번째 사용자 이름을 가져옴
//      UserName := CurrentRoomUsers[i];
//
//      // 사용자 이름으로 표시할 이미지 파일 경로
//      UserImageFile := GetUserImage(UserName);
//
//      if FileExists(Image_Folder + UserImageFile) then
//         ImgProfile.Picture.LoadFromFile(Image_Folder + UserImageFile)
//      else if FileExists(Image_Folder + 'default_profile.png') then
//         ImgProfile.Picture.LoadFromFile(Image_Folder + 'default_profile.png');
//
//      ImgProfile.BringToFront;
//      Inc(ImgX, 55);
//   end;
end;

//========초대 가능한 유저 요청에 대한 응답 ===========
procedure TChatRoomForm.HandleInviteAvailableUsers(Sender: TObject; const Msg: string);
var
   JSONObject   : TJSONObject;
   UsersArray   : TJSONArray;
   i            : Integer;
   UserName     : string;
begin

   if not Assigned(InvitePanel) then
   begin
      // 패널 생성
      InvitePanel                := TPanel.Create(Self);
      InvitePanel.Parent         := Self;
      InvitePanel.SetBounds(100, 100, 300, 400); // 위치와 크기
      InvitePanel.BevelOuter     := bvRaised;
      InvitePanel.Caption        := '초대할 유저 선택';

      // 체크리스트박스 생성
      CLBAvailableUsers          := TCheckListBox.Create(Self);
      CLBAvailableUsers.Parent   := InvitePanel;
      CLBAvailableUsers.SetBounds(10, 30, 280, 300);

      // 초대 버튼 생성
      BtnInvite            := TButton.Create(Self);
      BtnInvite.Parent     := InvitePanel;
      BtnInvite.SetBounds(100, 340, 100, 30);
      BtnInvite.Caption    := '초대하기';
      BtnInvite.OnClick    := BtnInviteClick; // 이벤트 연결
   end;

   JSONObject   := TJSONObject.ParseJSONValue(Msg) as TJSONObject;
   try
      if JSONObject = nil then Exit;
         UsersArray := JSONObject.GetValue('users') as TJSONArray;   //users 의 값 = 초대가능한 유저 이름을 가지고 있음
      if UsersArray = nil then Exit;

      CLBAvailableUsers.Items.Clear;                                //CLB초대 가능유저 초기화
         for i := 0 to UsersArray.Size - 1 do                       //초대 가능한 유저만큼 반복
      begin
         UserName := UsersArray.Get(i).Value;                       //초대 가능한 유저 이름을 저장
         CLBAvailableUsers.Items.Add(UserName);                     //CLB에 추가
      end;

      InvitePanel.Visible := True; // 패널 띄우기
   finally
      JSONObject.Free;
   end;
end;

//초대하기 버튼 클릭시
procedure TChatRoomForm.BtnInviteClick(Sender: TObject);
var
   i: Integer;
begin
   if not Assigned(CLBAvailableUsers) then Exit;

   for i := 0 to CLBAvailableUsers.Count - 1 do
   begin
      if CLBAvailableUsers.Checked[i] then
         Con.SendJson('INVITE', [
            'user', CLBAvailableUsers.Items[i],   // 초대할 유저 이름
            'room', CurrentRoomName               // 현재 방 이름
            ]);

   end;

   // 초대 후 패널 숨기기
   InvitePanel.Visible := False;
end;

procedure TChatRoomForm.AddUser(const UserName: string);
begin
   // CurrentRoomUsers 리스트가 없으면 생성
   if not Assigned(CurrentRoomUsers) then
      CurrentRoomUsers := TStringList.Create;

   // 이미 목록에 있는지 확인하여 중복 추가 방지
   if CurrentRoomUsers.IndexOf(UserName) = -1 then
   begin
      // 목록(데이터)에 새 사용자 추가
      CurrentRoomUsers.Add(UserName);

      // UI 업데이트: 참여자 수 레이블 갱신
      if Assigned(lblUserCount) then
         lblUserCount.Caption := Format('Users: %d', [CurrentRoomUsers.Count]);

      // UI 업데이트: 상단 프로필 이미지 다시 그리기
      UpdateUserImageUI;
  end;
end;

end.
